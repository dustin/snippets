#!/bin/sh
# Copyright (c) 1997  SPY Internetworking <dustin@spy.net>
# $Id: discover,v 1.8 2000/08/10 05:40:24 dustin Exp $ \
exec scotty -nf $0 $*

mib load rfc1213.mib
mib load rfc1696.mib
mib load cisco.mib
mib load cisco-smi.mib
mib load CISCO-CDP-MIB-V1SMI.mib

# set donePeers() ()
# set communities() ()

set node_id 0

# set nodes() ()
# set nodes_rev() ()

set links {}

set opmode text

# load the communities from the communities file
proc loadCommunities { } {
	global communities
	# set the default default community
	set communities(-default-) public
	set file "communities"
	if [ catch {open $file} input ] {
		return
	}
	while { [gets $input line] != -1} {
		set host [lindex $line 0]
		set comm [lindex $line 1]
		set communities($host) $comm
	}
	close $input
}

proc nextNode { } {
	global node_id
	return [incr node_id]
}

proc getNode { host } {
	global nodes
	global nodes_rev
	if { [ catch { set this_node $nodes($host) } ] } {
		set this_node [nextNode]
		set nodes($host) $this_node
		set nodes_rev($this_node) $host
	}
	return $this_node
}

proc getColor { media } {
	set m [ string tolower $media ]
	set color black
	if { [ string first gigabit $m ] >= 0 } {
		set color red1
	} elseif { [ string first ethernet $m ] >= 0 } {
		set color blue1
	} elseif { [ string first serial $m ] >= 0 } {
		set color green3
	}

	return $color
}

proc link { from to media} {
	global links
	set from_node [getNode $from]
	set to_node [getNode $to]

	if {$from_node > $to_node} {
		set a $to_node
		set b $from_node
	} else {
		set a $from_node
		set b $to_node
	}

	lappend links [ list $a $b [getColor $media] ]
}

# get the community for a host
proc getCommunity { host } {
	global communities
	set community $communities(-default-)
	catch { set community $communities($host) }
	return $community
}

# Get a list of all CDP peers off of the passed in interface for a given
# snmp session.
proc getPeers { s i } {
	set what "cdpCachePlatform.$i"
	set ret {}
	catch {
		$s walk vbl $what {
			set vb    [lindex $vbl 0]
			set oid [lindex $vb  0]
			set id [lindex [ split $oid . ] end]
			lappend ret $id
		}
	}
	return $ret
}

# Find out what the name of the peer is on an interface.  We do a bunch of
# crap to make sure it's a usable name.
proc peerName { s i peer } {
	set what "cdpCacheDeviceId.$i.$peer"
	catch {set name [lindex [lindex [$s get $what] 0] 2]}

	# This is a hack for those cisco switches that put ()'s in the name
	# set name [ join $newparts . ]
	set n [ string first \( $name ]
	set e [ string last \) $name ]
	if { $n > 0 } {
		if { $e > 0 } {
			set e [expr $e - 1 ]
		} else {
			set e "end"
		}
		set name [ string range $name [expr $n + 1 ] $e ]
	}
	return $name
}

# Get the peer's IP address
proc peerAddr { s i peer } {
	set what "cdpCacheAddressType.$i.$peer"
	set type 0
	set raddr 0
	catch {set type [lindex [lindex [$s get $what] 0] 2]}

	if { $type == 1 } {
		set what "cdpCacheAddress.$i.$peer"
		catch {set addr [lindex [lindex [$s get $what] 0] 2]}
		scan $addr "%x:%x:%x:%x" a b c d

		set raddr "$a.$b.$c.$d"
	} else {
		error "Not IP ($type)!"
	}

	return $raddr
}

# Display the peer
proc displayPeer { s i peer offset name } {
	global opmode
	if { [ string compare $opmode tki ] == 0 } {
		return
	}
	set what "cdpCachePlatform.$i.$peer"
	set code [catch {set platform [lindex [lindex [$s get $what] 0] 2]}]
	set what "cdpCacheDevicePort.$i.$peer"
	set code [catch {set port [lindex [lindex [$s get $what] 0] 2]}]

	puts "[makeRoom $offset]$name@$port ($platform)"
	flush stdout
}

# Create a space offset so's that we can read this.
proc makeRoom { offset } {
	set r ""
	for { set i 0 } { $i < $offset } { incr i } {
		set r "$r  "
	}
	return $r
}

# This does the actual work.
proc walk { offset host addr } {
	# puts "Walking $host"
	global donePeers
	global opmode

	set donePeers($addr) 1

	set pt [lindex [lindex [icmp echo $addr] 0] 1]

	if { $pt>-1 } {

		set community [getCommunity $addr]
		set code [catch {snmp session -timeout 15 -address $addr \
			-community $community} s]

		set what "ifNumber.0"
		set code [catch {set nint [lindex [lindex [$s get $what] 0] 2]}]
		if $code {
			set nint 0
		}

		# Find all interfaces.
		set interfaces {}
		catch {
			$s walk vbl "ifIndex" {
				set vb    [lindex $vbl 0]
				set oid [lindex $vb  0]
				set id [lindex [ split $oid . ] end]
				lappend interfaces $id
			}
		}

		# flip through all interfaces figuring out whether they're worthy
		# or not.
		foreach i $interfaces {
			# We only care about operational interfaces.
			set what "ifOperStatus.$i"
			set code [catch {set ans [lindex [lindex [$s get $what] 0] 2]}]
			if {[string compare $ans "up"] == 0} {

				set what "ifDescr.$i"
				set code [catch {set desc [lindex [lindex [$s get $what] 0] 2]}]
				if $code {
					set desc "$i"
				}

				# If we're not in tki mode
				if { [ string compare $opmode tki ] != 0 } {
					puts "[makeRoom $offset]$host $desc ($i)"
					flush stdout
				}

				set peers [ getPeers $s $i ]
				foreach peer $peers {
					set name [ peerName $s $i $peer ]
					set addr [ peerAddr $s $i $peer ]
					if { [ string length $name ] < 1 } {
						set name $addr
					}
					displayPeer $s $i $peer [expr $offset + 1 ] $name
					set doneit 0
					catch { set doneit $donePeers($addr) }
					if { $doneit == 0 } {
						walk [expr $offset + 2 ] $name $addr
						link $host $name $desc
					}
				}
			# Operational interfaces
			}

		# For each interface
		}
	# If we could ping the thing
	}
# for each router
}

proc makeTki { } {
	global nodes
	global links
	puts "##
## This file was created by tkined version 1.3.4        >> DO NOT EDIT <<
##
## This may look like TCL code but it is definitely not!
##

ined page A4 landscape

"

	foreach v [array names nodes ] {
		set node_id node$nodes($v)
		puts "set $node_id \[ ined -noupdate create NODE \]"
		puts "ined -noupdate icon \$$node_id cisco.xbm"
		puts "ined -noupdate name \$$node_id $v"
		puts "ined -noupdate label \$$node_id name"
		puts ""
		flush stdout
	}
	set i 0
	foreach l $links {
		incr i
		set a [lindex $l 0]
		set b [lindex $l 1]
		set color [lindex $l 2]
		puts "set link$i \[ ined -noupdate create LINK \$node$a \$node$b \]"
		puts "ined -noupdate color \$link$i $color"
		puts ""
		flush stdout
	}
}

# Figure out what we're doing
catch { set opmode [lindex $argv 1] }

# Load the communities
loadCommunities

# Start with an offset of 0 and whatever host we said to start on (twice,
# since we need the IP address, too)
walk 0 [lindex $argv 0] [lindex $argv 0]

if { [string compare $opmode "tki" ] == 0 } {
	makeTki
}

exit 0
