#!/usr/local/bin/wish8.3
# Copyright (c) 1999  Dustin Sallings <dustin@spy.net>
# $Id: 1921graph.tk,v 1.5 2000/07/19 08:56:17 dustin Exp $

set width 640
set height 480

proc ctof { c } {
	return [expr ($c*9/5)+32]
}

proc showGraph { width height } {
	global dumpData

	set min_t $dumpData(mission_start)
	set max_t [expr $min_t+($dumpData(sample_rate)*60*$dumpData(mission_samples))]
	set items 0

	set min_v  99999
	set max_v -99999

	set rate $dumpData(sample_rate)

	set i 0
	foreach key [lsort [array names dumpData sample_data_*]] {
		set value [ctof $dumpData($key) ]

		set mykey [expr $min_t+($rate*$i*60)]

		set input($mykey) $value

		# Set the min for the values
		if { $value < $min_v } {
			set min_v $value
		}

		# set the max for the values
		if { $value > $max_v } {
			set max_v $value
		}

		incr items
		incr i
	}

	# the y factor
	set yfactor [expr ($height-40)/($max_v-$min_v+1)]
	# the x factor
	puts "max_t=$max_t, min_t=$min_t ([expr $max_t-$min_t])"
	set xfactor [expr (1.0*($width-40.0))/(1.0*($max_t-$min_t+$rate))]

	set nadj [expr ($min_t-$min_t)]
	set minx [expr $width-(($width-40)-($nadj*$xfactor))]
	set nadj [expr ($max_t-$min_t)]
	set maxx [expr $width-(($width-40)-($nadj*$xfactor))]

	set miny [expr ($height-40-($max_v-$min_v)*$yfactor)]
	set maxy [expr ($height-20)]

	# Draw the box.
	.c create line $minx $miny $minx $maxy
	.c create line $maxx $miny $maxx $maxy
	.c create line $minx $miny $maxx $miny
	.c create line $minx $maxy $maxx $maxy

	puts "($minx,$miny) to ($maxx, $maxy)"

	# Find out the stuff for the graph.
	set starttime [clock format $dumpData(mission_start) ]
	puts "Mission start time:  $starttime"
	# Figure out how many seconds we have to graph
	set timespan [expr ($rate*60*$dumpData(mission_samples))]
	puts "OK, looks like we have $timespan seconds of data"
	# Find the first even hour
	set firstline [expr (($dumpData(mission_start) / 3600)+1) * 3600 ]
	puts "First time:  [clock format $firstline]"

	######################################################################
	# Time lines
	######################################################################

	# Draw the hour lines.
	for { set i $firstline } { $i < $max_t } { set i [expr $i + 3600 ] } {
		set nadj [expr ($i-$min_t)]
		set x [expr $width-(($width-40)-($nadj*$xfactor))]
		# puts "Drawing line for  [clock format $i] ($i, max is $max_t)"
		.c create line $x $maxy $x $miny -dash . -fill grey
	}

	set firstline [expr (($dumpData(mission_start) / 86400)+1) * 86400 ]
	# Draw the day lines.
	for { set i $firstline } { $i < $max_t } { set i [expr $i + 86400 ] } {
		set nadj [expr ($i-$min_t)]
		set x [expr $width-(($width-40)-($nadj*$xfactor))]
		.c create line $x $maxy $x $miny -fill grey
		.c addtag text withtag [.c create text $x [expr ($height-10)] \
			-text [clock format $i -format %m/%d]]
	}

	######################################################################

	######################################################################
	# Temp lines
	######################################################################

	set dashwidth 2
	# Draw the $dashwidth-degree lines.
	set v [expr (int($min_v/$dashwidth)*$dashwidth)+$dashwidth]
	for { set i $v } { $i < $max_v } { set i [expr $i + $dashwidth]} {
		set nadj [expr ($i-$min_v)]
		set y [expr ($height-40)-($nadj*$yfactor)]
		.c create line $minx $y $maxx $y -dash . -fill grey
	}

	# Draw the ten-degree lines.
	set v [expr (int($min_v/10)*10)+10]
	for { set i $v } { $i < $max_v } { set i [expr $i + 10 ] } {
		set nadj [expr ($i-$min_v)]
		set y [expr ($height-40)-($nadj*$yfactor)]
		.c create line $minx $y $maxx $y -fill grey
		.c addtag text withtag [.c create text 20 $y \
			-text $i]
	}

	######################################################################

	set lastx -1

	for { set i $min_t } { $i < $max_t } { set i [expr $i + ($rate*60)] } {
		set n $input($i)

		# set x [expr ($i+1) * $xfactor ]
		set nadj [expr ($i-$min_t)]
		set x [expr $width-(($width-40)-($nadj*$xfactor))]
		set nadj [expr ($n-$min_v)]
		set y [expr ($height-40)-($nadj*$yfactor)]

		if { $lastx >= 0 } {
			.c create line $lastx $lasty $x $y -width 1
		}

		set lastx $x
		set lasty $y
	}
}

proc showHistogram { width height } {
	global dumpData

	set input {}
	# Find the histogram entries.
	foreach key [lsort [array names dumpData histogram_*]] {
		set itmp [split $dumpData($key) {, }]
		set i [list [lindex $itmp 0 ] [ lindex $itmp 2 ] [ lindex $itmp 4] ]
		lappend input $i
	}

	set max 0
	set i 0
	set firsttemp 0
	set lasttemp 0
	set items 0
	foreach datum $input {
		set n [lindex $datum 2]
		# Set the max
		if {$n > $max} {
			set max $n
		}
		# Find the first temp
		if { ($firsttemp == 0) && ($n > 0) } {
			set firsttemp $i
		}
		# find the last temp
		if { $n > 0 } {
			set lasttemp $i
		}
		# If it's valid, count it
		if { $n>0 } {
			incr items
		}
		incr i
	}

	# The yfactor varies by the maximum value in the histogram
	set yfactor [expr ($height-20)/$max]
	# The xfactor varies by the number of valid data points in the histogram
	set xfactor [expr ($width-20)/$items]
	# The width varies by the number of items
	set bwidth [expr ($xfactor/2)-($xfactor*.10)]
	for { set i $firsttemp } { $i < $lasttemp } { incr i } {

		set datum [lindex $input $i ]

		set min [lindex $datum 0]
		set max [lindex $datum 1]
		set n [lindex $datum 2]

		set x [ expr ($i-$firsttemp+1) * $xfactor ]
		set y [expr ($height-20)-($n*$yfactor)]

		set color white
		if { $min < $dumpData(low_alarm) } {
			set color blue
		}
		if { $max > $dumpData(high_alarm) } {
			set color red
		}

		.c create rectangle [expr $x-$bwidth] [expr $height-20] \
			[expr $x+$bwidth] $y \
			-fill $color -width 1 -outline black

		.c addtag text withtag [.c create text $x [expr ($height-10)] \
			-text [ctof $min]]
		.c addtag text withtag [.c create text $x [expr ($y+10)] \
			-text $n]
	}
}

proc readDump { file } {
	global dumpData

	set fd [ open $file r ]

	catch { unset dumpData }

	while { [ gets $fd line ] != -1 } {
		set error [ catch {
			set kv [split $line =]
			set dumpData([lindex $kv 0]) [lindex $kv 1]
		} ]
		if { $error } {
			puts "Damnit:  $error"
		}
	}

	close $fd
}

wm title . "Temperature Graph"
wm iconname . "1921"
canvas .c -relief raised -width $width -height $height
pack .c -side top -fill x

readDump /tmp/dump
# showHistogram $width $height
showGraph $width $height
